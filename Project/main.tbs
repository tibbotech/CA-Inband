
'------------------------------------------------------------------------------------------------------
'										Inband command Demo
'------------------------------------------------------------------------------------------------------
'The purpose of this demo program is to show users, how to redirect Ethernet data to serial
'How does this demo work?
'1. Load this program into the device
'2. Open a telnet session, (type "telent" under windows "Start"->"Run")
'3. In the telnet window, type "open 192.168.1.104 1001"
'4. Connect the device to a COM port on the computer.
'5. Open HyperTerminal session, select this COM port and set up the program with the following settings:		
'			Baudrate = 38400
'			Data bits = 8	   
'			Stop bit = 1
'			Flow control = none
'			Parity = none
'6. Now whatever you typed in the telnet, should show up in HyperTerminal, and vice versa.
'   To test inband commands, type "$" in the telnet window, and follow with a LED pattern.
'	End the pattern with "%"
'NOTICE, LED pattern should only consist of "G", "R", "B", "-", "~", "*", please refer to the online
'help for the PAT object.
'
'
'                  Since Taiko R.2, the redir property only work under EM1000
'
'RD.042007
'------------------------------------------------------------------------------------------------------


dim s as string	
sub on_sys_init
	dim x as byte

	x=(sys.totalbuffpages-2)/5
 
'set sock cmd, rpl buffer 
	sock.cmdbuffrq(1)
	sock.rplbuffrq(1)

'set serial tx and rx buffer
	ser.txbuffrq(x)
	ser.rxbuffrq(x)

'set Sock buff
	sock.tx2buffrq(x)
	sock.rxbuffrq(x)
	sock.txbuffrq(x)
		
	sys.buffalloc

	sock.redir(PL_REDIR_SER)		'redirect sock data to serial
	ser.redir(PL_REDIR_SOCK0)		'redirect serial data to sock

'set sock tcp protocol 
	sock.protocol=PL_SOCK_PROTOCOL_TCP
	sock.inconmode = PL_SOCK_INCONMODE_ANY_IP_ANY_PORT
	sock.reconmode=PL_SOCK_RECONMODE_2
	sock.inbandcommands = YES
	sock.localportlist = "1001"
	sock.escchar = 36			'inband command starting character, $
	sock.endchar = 37			'inband command endding character, %

'set IP address
	net.ip = "192.168.1.104"  '<------------ you may need to change this
	
'set serial baudrate
	ser.baudrate = ser.div9600/(38400/9600)
	ser.enabled = 1
end sub

sub on_sock_inband
	s = sock.getinband
	s = left(s,len(s)-1)
	pat.play(s,YES)	
end sub


